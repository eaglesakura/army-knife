/**
 * 0.1.x  : playground-version.
 */
def ARTIFACT_VERSION = System.env.CIRCLE_TAG == null ?
        "0.1" : System.env.CIRCLE_TAG.substring(System.env.CIRCLE_TAG.indexOf('v') + 1)
def BINTRAY_LABELS = ["android", "kotlin"]

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.eaglesakura.ci-support'

group = "com.eaglesakura"
version = ARTIFACT_VERSION + getArtifactVersionSuffix()

if (project.hasProperty("devBuild")) {
    ext.ANDROID_STUDIO = true
    ext.ANDROID_BUILD_NUMBER = 1
    ext.ANDROID_AAPT_CRUNCHER = false
} else {
    ext.ANDROID_STUDIO = false
    ext.ANDROID_BUILD_DATE = "\"${eglibrary.ci.buildDate}\""
    ext.ANDROID_BUILD_NUMBER = eglibrary.ci.buildVersionCode as int
    ext.ANDROID_AAPT_CRUNCHER = true
}

dependencies {
    /**
     * Kotlin support
     */
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5'

    /**
     * Support Libraries
     * https://developer.android.com/topic/libraries/architecture/adding-components
     * https://developer.android.com/topic/libraries/support-library/refactor
     */
    api 'androidx.core:core-ktx:1.0.0-beta01'
    api 'androidx.appcompat:appcompat:1.0.0-beta01'
    api "androidx.lifecycle:lifecycle-extensions:2.0.0-beta01"
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0-beta01'
    androidTestImplementation 'androidx.multidex:multidex-instrumentation:2.0.0'

    /**
     * for Tests
     */
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'androidx.test:monitor:1.1.0-alpha4'

    /**
     * for Android Tests
     */
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

android {
    compileSdkVersion 28
    aaptOptions.cruncherEnabled = ANDROID_AAPT_CRUNCHER

    // for *.java options
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // for Kotlin-Android
    androidExtensions {
        experimental = true
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        multiDexEnabled true
        aaptOptions.cruncherEnabled false
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
}


static def getArtifactVersionSuffix() {
    String CIRCLE_TAG = System.env.CIRCLE_TAG

    if (CIRCLE_TAG != null) {
        // have CIRCLE_TAG(build on CircleCI)
        return ""
    }

    if (System.env.CIRCLE_BUILD_NUM != null) {
        return ".build-${System.env.CIRCLE_BUILD_NUM}"
    } else {
        return ".snapshot"
    }
}

apply plugin: 'com.jfrog.bintray'
bintray {
    user = 'eaglesakura'
    key = System.env.BINTRAY_API_KEY
    publish = true
    override = true
    pkg {
        repo = "maven"
        name = file(".").absoluteFile.name
        vcsUrl = System.env.CIRCLE_REPOSITORY_URL
        licenses = ["MIT"]
        labels = BINTRAY_LABELS
        version {
            name = project.version
            released = new Date()
            gpg {
                sign = true
                passphrase = System.env.BINTRAY_GPG_PASS
            }
        }
    }
    configurations = ['archives']
}